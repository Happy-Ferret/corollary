use std::str::FromStr;
use ast::*;
use lalrpop_util::ErrorRecovery;
use decode_literal;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, (usize, &'input str), ()>>);

pub Exprs = Comma<Expr>;

Dot<T>: Vec<T> = { // (0)
    <v:(<T> ".")+> <e:T> => { // (1)
        let mut v = v;
        v.push(e);
        v
    }
};

Semi<T>: Vec<T> = { // (0)
    <v:(<T> ";")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Comma<T>: Vec<T> = { // (0)
    <v:(<T> ",")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Pipe<T>: Vec<T> = { // (0)
    <v:(<T> "|")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Commaish<T>: Vec<T> = { // (0)
    <(<T> ","?)+> => <>,
};

CommaDef<T>: Vec<T> = { // (0)
    <v:(<T> ",")*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    },
};

Quote: String = {
    r#""[^"]*""# => {
      let a = <>;
      a[1..a.len()-1].to_string()
    }
};

SingleQuote: String = {
    r"'[^']*'" => {
      let a = <>;
      a[1..a.len()-1].to_string()
    }
};


Ident: Ident = {
  r"[a-zA-Z_.][.a-zA-Z_0-9':=]*" => Ident((<>).to_string()),
};

Ctor: Ident = {
    r":[!#$%&*+./<=>?@^|~:\\-]*" => Ident(<>.to_string()),
};

Operator: String = {
    r"[!#$%&*+/<=>?@^|~][!#$%&*+./<=>?@^|~:\\-]*" => <>.to_string(),
    "." => <>.to_string(),
    ".." => <>.to_string(),
    ".|." => <>.to_string(),
    ".&." => <>.to_string(),
    "-" => <>.to_string(),
};

Section: i32 = {
    Ident ("(" ".." ")")? => 0,
    Ident "(" Ident* ")" => 0,
    "module" Ident ("(" Comma<Section> ")")? => 0,
};

Case: CaseCond = {
    // TODO commas can be used in expr spans, see AST.hs
    <d:PatList> <v:("|" <CommaDef<ExprSpan>> "->" <ExprSpan>)*> => CaseCond::Matching(d, v),
    <d:PatList> "->" <v:CommaDef<ExprSpan>> ("where" "{" Semi<Statement> "}")? => CaseCond::Direct(d, v),
    "where" "{" <Semi<Statement>> "}" => CaseCond::Where,
};


ExprSpan: Expr = {
    <Expr+> => Expr::Span(<>),
};

Expr: Expr = {
    Quote => Expr::Str(decode_literal(&<>)),
    SingleQuote => Expr::Char(decode_literal(&<>)),
    <Ident> => Expr::Ref(<>),
    Num => Expr::Number(<>),

    r"\\" "(" CommaDef<ExprSpan> ")" "->" Expr => Expr::Lambda,
    "[" <CommaDef<ExprSpan>> "|" CommaDef<ExprSpan> "]" => Expr::Dummy,
    "[" <CommaDef<ExprSpan>> "]" => Expr::Vector(<>),
    "[" "]" => Expr::Vector(vec![]),
    "(" <CommaDef<ExprSpan>> ")" => Expr::Parens(<>),
    "(" ")" => Expr::Parens(vec![]),
    "(" "," ")" => Expr::Dummy,
    "(" Ident "->" PatList ")" => Expr::Dummy,

    r"\\" r"[a-zA-Z_.][.a-zA-Z_0-9':=]*" Expr* "->" Expr => Expr::Lambda,

    "`" <Ident> "`" => Expr::Operator(<>.0),
    <Ctor> => Expr::Operator(<>.0),
    <Operator> => Expr::Operator(<>),
    "$" => Expr::Operator(<>.to_string()),
    "==" => Expr::Operator(<>.to_string()),
    r"\\\\" => Expr::Operator(<>.to_string()),

    // these special operators should not be exprs:
    "::" => Expr::Operator(<>.to_string()),
    "@" => Expr::Operator(<>.to_string()),

    "case" <e:ExprSpan> "of" "{" <s:Semi<Case>> "}" => Expr::Case(Box::new(e), s),

    "let" "{" Semi<LetInner> "}" => Expr::Let,

    "do" "{" <e:Semi<LetInner>> "}" => Expr::Do(vec![], None),

    <i:Ident> "{" <r:Comma<(<Ident> "=" <ExprSpan>)>> "}" => Expr::Record(i, r),
};

// TODO combine this with Statement + Expr for proper AST
LetInner: () = {
  "class" Type ("=>" Ident+)?
    "where" "{" Semi<Statement> "}" => (),

  "instance" FnDef
    "where" "{" Semi<Statement> "}" => (),
  "import" ImportList+ => (),

  "data" <id:Ident+> "=" <p:Pipe<TypeGroup>>
    <derives:("deriving" <ImportList>)?> =>
      (),
  "newtype" <id:Ident+> "=" <p:Type>
    ("deriving" ImportList)? =>
      (),

  "type" <d:Ident> <args:TypeGroup?> "=" TypeGroup
    ("where" "{" Semi<Statement> "}")? => (),

  <ExprSpan> "=" <ExprSpan> => (),
  <ExprSpan> "|" <ExprSpan> "=" <ExprSpan> ("where" "{" Semi<Statement> "}")? => (),
  <ExprSpan> ("where" "{" <Semi<Statement>> "}")? => (), // TODO this should be associated with parent "do", not with its inner contents

  "where" "{" <Semi<Statement>> "}" => (),
};

FnDef: Ty = {
    //<a:Type> "=>" <b:Type> => Ty::Where(Box::new(a), Box::new(b)),

    <a:Type> "=>" <b:Type> => b,
    <Type> => <>,
};

Type: Ty = {
    <a:TypeSpan> "->" <b:Type> => Ty::Pair(Box::new(a), Box::new(b)),
    <TypeSpan> => <>,
};

TypeRecord: () = {
  <Ident> "::" <TypeGroup> => (),
};

TypeSub: Ty = {
    "!" <TypeSub> => Ty::Not(Box::new(<>)),
    "[" <Type> "]" => Ty::Brackets(Box::new(<>)),
    "(" <CommaDef<Type>> ")" => Ty::Tuple(<>),
    "(" ")" => Ty::EmptyParen,
    "{" <Comma<TypeRecord>> "}" => Ty::RecordTODO,
    <Ident> => Ty::Ref(<>),
    <Ctor> => Ty::Ref(<>),
};

TypeGroupInner: Ty = {
  <a:TypeSub> "=>" <b:TypeSub> => b,
  <TypeSub> => <>,
};

TypeGroup: Vec<Ty> = {
    <TypeGroupInner+> => <>,
};

TypeSpan: Ty = {
    <TypeSub+> => Ty::Span(<>),
};


PatSub: Pat = {
    "!" <PatSub> => Pat::Not(Box::new(<>)),
    "[" <CommaDef<PatSpan>> "]" => Pat::Brackets(<>),
    "[" "]" => Pat::Brackets(vec![]),
    "(" <PatSpan> ")" => <>,
    "(" <h:PatSpan> "," <t:CommaDef<PatSpan>> ")" => {
        let mut vec = vec![h];
        vec.extend_from_slice(&t);
        Pat::Tuple(vec)
    },
    "(" <i:Ident> "->" <s: PatSpan> ")" => Pat::Arrow(i, Box::new(s)),
    "(" ")" => Pat::EmptyParen,
    "{" <Comma<PatRecord>> "}" => Pat::RecordTODO,
    <Ident> => Pat::Ref(<>),
    <Ctor> => Pat::Ref(<>),
    <Operator> => Pat::Ref(Ident(<>)),
    <Quote> => Pat::Str(decode_literal(&<>)),
    <SingleQuote> => Pat::Char(decode_literal(&<>)),
    <Num> => Pat::Num(<>),
    "==" => Pat::Dummy,
    "@" => Pat::Dummy,

    // HACK
    "`" Ident "`" => Pat::Dummy,
};

PatRecord: () = {
  <Ident> "=" <PatSpan> => (),
  ".." => (), // RecordWildCards
};

// for matching (Ctor arg arg ...)
PatSpan: Pat = {
    <PatList> => Pat::Span(<>),
};

// for matching e.g. multiple function args
PatList: Vec<Pat> = {
    <PatSub+> => <>,
};


ImportList: Vec<Ident> = {
    "(" <a:Commaish<(<Ident> ("(" ".." ")")?)>> ")" => a,
    "(" ")" => vec![],
    <Ident> => vec![<>],

    "(" "(<|>)" ")" => vec![], // ??? see DefTable.hs
};

pub Module: Module = {
  "module" <n:Ident> ("(" Comma<Section> ")")?
    "where" "{" <s:Semi<Statement>> "}" => Module {
      name: n,
      statements: s,
    },
};

Statement: Statement = {
  "class" Type ("=>" Ident+)?
    "where" "{" Semi<Statement> "}" => Statement::Class,

  "instance" FnDef
    "where" "{" Semi<Statement> "}" => Statement::Instance,
  "import" ImportList+ => Statement::Import,

  // TODO change data enum to support this
  "data" <id:Ident+> => Statement::Class,

  "data" <id:Ident+> "=" <p:Pipe<TypeGroup>>
    <derives:("deriving" <ImportList>)?> =>
      Statement::Data(id[0].clone(), p, derives.unwrap_or(vec![])),
  "newtype" <id:Ident+> "=" <p:Type>
    ("deriving" ImportList)? =>
      Statement::Newtype,

  <i:Ident> "::" <d:FnDef> => Statement::Prototype(i, vec![d]),

  "type" <d:Ident> <args:TypeGroup?> "=" TypeGroup
    ("where" "{" Semi<Statement> "}")? => Statement::Typedef(d),

  <args:PatList> ("|" Commaish<Expr> "=" Expr+)*
    ("where" "{" Semi<Statement> "}")? => Statement::GuardAssign,

  <args:PatList> "=" <e:ExprSpan>
    ("where" "{" Semi<Statement> "}")? => Statement::Assign(args[0].clone(), args[1..].to_vec(), e),
};

Num: isize = {
    r"[0-9]+" => isize::from_str(<>).unwrap()
};
